buildscript {
    repositories {
        mavenCentral()
    }

    ext.kotlin_version = '1.0.0'

    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE')
        classpath('se.transmode.gradle:gradle-docker:1.2')
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
// tag::plugin[]
apply plugin: 'docker'
// end::plugin[]

// This is used as the docker image prefix (org)
group = 'skinny'
version = '0.2.0'

jar {
    archiveName "${baseName}.${extension}"
}

task buildDocker(type: Docker, dependsOn: build) {
  push = false
  applicationName = project.name
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.assertj:assertj-core:3.3.0"
    testCompile 'org.jetbrains.spek:spek:0.1.195'
    testCompile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
